buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.application'

tasks.register("createResRaw") {
    mkdir "res/raw"
}

tasks.register("zipDemos", Zip) {
    group = "Android"
    description = "Zip the demo patches to resources"
    archiveFileName = "demos.zip"
    destinationDirectory = layout.projectDirectory.dir("res/raw");
    from "droidparty-demos"
    dependsOn("createResRaw")
}

tasks.register("zipAbs", Zip) {
    group = "Android"
    description = "Zip the bundle abstractions to resources"
    archiveFileName = "abstractions.zip"
    destinationDirectory = layout.projectDirectory.dir("res/raw");
    from "bundled-abstractions"
    dependsOn("createResRaw")
}

android {
    namespace = 'cx.mccormick.pddroidparty'
    compileSdkVersion 34
    ndkVersion = '24.0.8215888'

    defaultConfig {
        applicationId "cx.mccormick.pddroidparty"
        minSdkVersion 17
        targetSdkVersion 34
        versionCode = project.hasProperty('versionCode') ? project.getProperty('versionCode').toInteger() : 1
        versionName = project.hasProperty('versionName') ? project.getProperty('versionName') : ""
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest.setRoot('tests')
    }

    signingConfigs {
        release {
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    tasks.named("preBuild") {
        dependsOn("zipDemos")
        dependsOn("zipAbs")
    }
}

dependencies {
    implementation 'androidx.core:core:1.1.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'io.github.libpd.android:pd-core:1.2.1-rc6'
}
