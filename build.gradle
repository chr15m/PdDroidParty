buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.1'
    }
}

allprojects {
    repositories {
        google()
        maven {
            name = 'Central Portal Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
        mavenCentral()
    }
}

apply plugin: 'com.android.application'

tasks.register("createResRaw") {
    mkdir "res/raw"
}

tasks.register("zipDemos", Zip) {
    group = "Android"
    description = "Zip the demo patches to resources"
    archiveFileName = "demos.zip"
    destinationDirectory = layout.projectDirectory.dir("res/raw");
    from "droidparty-demos"
    dependsOn("createResRaw")
}

tasks.register("zipAbs", Zip) {
    group = "Android"
    description = "Zip the bundle abstractions to resources"
    archiveFileName = "abstractions.zip"
    destinationDirectory = layout.projectDirectory.dir("res/raw");
    from "bundled-abstractions"
    dependsOn("createResRaw")
}

android {
    namespace = 'cx.mccormick.pddroidparty'
    compileSdkVersion 34
    ndkVersion = '29.0.14206865'

    defaultConfig {
        applicationId "cx.mccormick.pddroidparty"
        versionCode = project.hasProperty('versionCode') ? project.getProperty('versionCode').toInteger() : 1
        versionName = project.hasProperty('versionName') ? project.getProperty('versionName') : ""

        minSdkVersion = 28
        targetSdkVersion = 34
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest.setRoot('tests')
    }

    signingConfigs {
        release {
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    tasks.named("preBuild") {
        dependsOn("zipDemos")
        dependsOn("zipAbs")
    }

    buildFeatures {
        // Extract native libs from prefab, to be able to access the headers and to link to the libs
        prefab = true
        viewBinding = true
    }

    packagingOptions {
        jniLibs {
            // since externals are loaded at native code level, they need to be copied to the actual
            // filesystem, instead of staying in the apk; so we must set useLegacyPackaging:
            useLegacyPackaging = true
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.5.0'

    def appcompat_version = "1.6.1"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def pdCoreVersion = "1.4.1-SNAPSHOT"
    implementation "io.github.libpd.android:pd-core:$pdCoreVersion"
}
